/////////////////////////////////////////////////////////////
//
// default_led_pattern.txt
// version 2.1.18
//
// This is the test pattern used to generate the factory default firmware pattern
//
//
/////////////////////////////////////////////////////////////
// compiler_version:v2 PLEASE DO NOT REMOVE OR ALTER THIS LINE
/////////////////////////////////////////////////////////////

define MAX_LEDS_PER_STRIP 8

// Define group numbers

define ALL_LEDS         1
define STRIP_NUMBER_ID  2

define FRONT            3
define BACK             4
define MIDDLE_FOUR      5
define CORNERS          6

define LEFT             7
define RIGHT            8

define FRONT_LEFT       9
define FRONT_RIGHT     10
define BACK_LEFT       11
define BACK_RIGHT      12

// Define flight modes

define MODE_STABILIZE   0
define MODE_ACRO        1
define MODE_ALT_HOLD    2
define MODE_AUTO        3
define MODE_RTL         6
define MODE_LAND        9
define MODE_TUNE       15
define MODE_POSHOLD    16

// Define standard colors

define RED            [255 0 0]
define GREEN          [0 255 0]
define BLUE           [0 0 255]
define YELLOW         [255 255 0]
define CYAN           [0 255 255]
define MAJENTA        [255 0 255]

define OFF            [0 0 0]	         
       
define WHITE_20       [50 50 50]	              // ~20% White	
define WHITE_80       [200 200 200]	            // ~80% White	
define RED_20         [50 0 0]                  // ~20% Red

initialize begin

	// group ALL_LEDS is used for all leds - usually this group is used for background
	append group ALL_LEDS as strip 1 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS as strip 2 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS as strip 3 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS as strip 4 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS as strip 5 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS as strip 6 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS as strip 7 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS as strip 8 led 1 to MAX_LEDS_PER_STRIP

	// group for strip number identification
	append group STRIP_NUMBER_ID as strip 1 led 1 to 1
	append group STRIP_NUMBER_ID as strip 2 led 1 to 2
	append group STRIP_NUMBER_ID as strip 3 led 1 to 3
	append group STRIP_NUMBER_ID as strip 4 led 1 to 4
	append group STRIP_NUMBER_ID as strip 5 led 1 to 5
	append group STRIP_NUMBER_ID as strip 6 led 1 to 6
	append group STRIP_NUMBER_ID as strip 7 led 1 to 7
	append group STRIP_NUMBER_ID as strip 8 led 1 to 8

	// groups for front, back, middle and corners
	append group FRONT as strip 1 led 1 to MAX_LEDS_PER_STRIP
	append group FRONT as strip 2 led 1 to MAX_LEDS_PER_STRIP
	append group BACK as strip 7 led 1 to MAX_LEDS_PER_STRIP
	append group BACK as strip 8 led 1 to MAX_LEDS_PER_STRIP
	append group MIDDLE_FOUR as strip 3 led 1 to MAX_LEDS_PER_STRIP
	append group MIDDLE_FOUR as strip 4 led 1 to MAX_LEDS_PER_STRIP
	append group MIDDLE_FOUR as strip 5 led 1 to MAX_LEDS_PER_STRIP
	append group MIDDLE_FOUR as strip 6 led 1 to MAX_LEDS_PER_STRIP  
	append group CORNERS as strip 1 led 1 to MAX_LEDS_PER_STRIP
	append group CORNERS as strip 2 led 1 to MAX_LEDS_PER_STRIP
	append group CORNERS as strip 7 led 1 to MAX_LEDS_PER_STRIP
	append group CORNERS as strip 8 led 1 to MAX_LEDS_PER_STRIP
 
 
  // groups for left and right
	append group LEFT as strip 1 led 1 to MAX_LEDS_PER_STRIP        
	append group LEFT as strip 3 led 1 to MAX_LEDS_PER_STRIP        
	append group LEFT as strip 5 led 1 to MAX_LEDS_PER_STRIP         
	append group LEFT as strip 7 led 1 to MAX_LEDS_PER_STRIP         
	append group RIGHT as strip 2 led 1 to MAX_LEDS_PER_STRIP        
	append group RIGHT as strip 4 led 1 to MAX_LEDS_PER_STRIP        
	append group RIGHT as strip 6 led 1 to MAX_LEDS_PER_STRIP        
	append group RIGHT as strip 8 led 1 to MAX_LEDS_PER_STRIP        
	   
  // groups for corner arms
	append group FRONT_LEFT as strip 1 led 1 to MAX_LEDS_PER_STRIP
	append group FRONT_RIGHT as strip 2 led 1 to MAX_LEDS_PER_STRIP
	append group BACK_LEFT as strip 7 led 1 to MAX_LEDS_PER_STRIP
	append group BACK_RIGHT as strip 8 led 1 to MAX_LEDS_PER_STRIP

end

loop begin
  //
  // One of the following if conditions will match depending on the value of channel 8.  When channel 8 is max, an auto mode is enabled and that uses flight modes to set LEDs
  //
  if ( rc.ch8 <= 1100 ) then 
    //
    // LED test - identify strip numbers (see documentation for which strip should be on which arm)
    //
    color group ALL_LEDS WHITE_20									 
    color group STRIP_NUMBER_ID RED_20							 
  elsif ( rc.ch8 <= 1200  ) then 
    color group ALL_LEDS OFF									 
    flash group ALL_LEDS YELLOW 30 970   0							 
    flash group ALL_LEDS YELLOW 20 980 200						 
  elsif ( rc.ch8 <= 1300  ) then 
    random group 1 200 10					 
    bar group FRONT_LEFT RED    25 0 100 1   
    bar group FRONT_RIGHT GREEN 25 0 100 1     
    bar group BACK_LEFT RED     25 0 100 1     
    bar group BACK_RIGHT GREEN  25 0 100 1   		
  elsif ( rc.ch8 <= 1400 ) then       // scotts nav pattern
    color group 1 #000000						 
    fill group FRONT_LEFT RED    150 1000 1
    fill group FRONT_RIGHT GREEN 150 1000 1
    fill group BACK_LEFT RED     150 1000 0
    fill group BACK_RIGHT GREEN  150 1000 0
  elsif ( rc.ch8 <= 1500 ) then
    random group 1 200 10					 
    wave group FRONT_LEFT RED    50 2	1
    wave group FRONT_RIGHT GREEN 50 2 1
    wave group BACK_LEFT RED     50 2 0
    wave group BACK_RIGHT GREEN  50 2 0			  
  elsif ( rc.ch8 <= 1600 ) then 
    color group ALL_LEDS #050505		
    bounce group FRONT_LEFT RED    100 1                 
    bounce group FRONT_RIGHT GREEN 100 1               
    bounce group BACK_LEFT RED     100 1              
    bounce group BACK_RIGHT GREEN  100 1   				        
  elsif ( rc.ch8 <= 1700 ) then
    //
    // Double flash left (red) to right (green)
    //
    color group  ALL_LEDS OFF						 
    flash group LEFT RED    20 950   0     
    flash group LEFT RED    20 950 100       
    flash group RIGHT GREEN 20 950 200        
    flash group RIGHT GREEN 20 950 300   							
  elsif ( rc.ch8 <= 1800 ) then     
    color group ALL_LEDS WHITE_80					
    bar group FRONT_LEFT RED    25 0 100  1   
    bar group FRONT_RIGHT GREEN 25 0 100  1     
    bar group BACK_LEFT RED     25 0 100  1     
    bar group BACK_RIGHT GREEN  25 0 100  1  
  elsif ( rc.ch8 <= 1900 ) then 
    //
    // Stealth mode
    //
    color group 1 OFF					
  else
    //
    // Auto mode
    // When ch8 is maxed, the LED mode is specified by flight mode (and braking)
    //
    color group ALL_LEDS OFF						 
    if ( fc.flightmode == MODE_STABILIZE ) then 
      color group ALL_LEDS CYAN						 
    elsif ( fc.flightmode == MODE_ALT_HOLD ) then
      color group CORNERS RED                     						   
    elsif ( fc.flightmode == MODE_POSHOLD ) then
      color group CORNERS GREEN						 
    elsif ( fc.flightmode == MODE_RTL ) then
      color group CORNERS BLUE						 
    elsif ( fc.flightmode == MODE_AUTO ) then
      color group CORNERS MAJENTA
    elsif ( fc.flightmode == MODE_LAND ) then
      color group CORNERS YELLOW						 
    else
      color group MIDDLE_FOUR WHITE_20               			 
    end
    if ( imu.brake > 20 ) then                       // Braking
      color group BACK RED                           // Back bright red							   
    end  
  end
  
end