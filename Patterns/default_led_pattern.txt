/////////////////////////////////////////////////////////////
//
// default_led_pattern.txt
//
// This is the test pattern used to generate the factory default firmware pattern
//
//
/////////////////////////////////////////////////////////////
// compiler_version:v2 PLEASE DO NOT REMOVE OR ALTER THIS LINE
/////////////////////////////////////////////////////////////

initialize begin

	// group 1 is used for all leds - usually this group is used for background
	append group 1 as strip 1 led 1 to 8
	append group 1 as strip 2 led 1 to 8
	append group 1 as strip 3 led 1 to 8
	append group 1 as strip 4 led 1 to 8
	append group 1 as strip 5 led 1 to 8
	append group 1 as strip 6 led 1 to 8
	append group 1 as strip 7 led 1 to 8
	append group 1 as strip 8 led 1 to 8

	// group 2 is used to identify the strip numbers
	append group 2 as strip 1 led 1 to 1
	append group 2 as strip 2 led 1 to 2
	append group 2 as strip 3 led 1 to 3
	append group 2 as strip 4 led 1 to 4
	append group 2 as strip 5 led 1 to 5
	append group 2 as strip 6 led 1 to 6
	append group 2 as strip 7 led 1 to 7
	append group 2 as strip 8 led 1 to 8

	// 3 = front, 4 = back
	append group 3 as strip 1 led 1 to 8
	append group 3 as strip 2 led 1 to 8
	append group 4 as strip 7 led 1 to 8
	append group 4 as strip 8 led 1 to 8

	// 5 = mid
	append group 5 as strip 3 led 1 to 8
	append group 5 as strip 4 led 1 to 8
	append group 5 as strip 5 led 1 to 8
	append group 5 as strip 6 led 1 to 8
  
  // Individual arms
	// 6 front left, 7 = front right, 8 = rear left, 9 = rear right
	append group 6 as strip 1 led 1 to 8
	append group 7 as strip 2 led 1 to 8
	append group 8 as strip 7 led 1 to 8
	append group 9 as strip 8 led 1 to 8
 
  // Left right
	// 10 = left, 11 = right
	append group 10 as strip 1 led 1 to 8         // left 
	append group 10 as strip 3 led 1 to 8         //  
	append group 10 as strip 5 led 1 to 8         //  
	append group 10 as strip 7 led 1 to 8         //  
	append group 11 as strip 2 led 1 to 8         // right 
	append group 11 as strip 4 led 1 to 8         //  
	append group 11 as strip 6 led 1 to 8         //  
	append group 11 as strip 8 led 1 to 8         //  
	  
end

if ( rc.ch8 <= 1100 ) then 
	color group 1 #101010									 
	color group 2 #100000									 
end
  
if ( 1100 < rc.ch8 and rc.ch8 <= 1200  ) then 
	color group 1 #000000									 
	flash group 1 [ 255 255 0 ] 30 970 0							 
	flash group 1 [ 255 255 0 ] 20 980	200						 
end
  
if ( 1200 < rc.ch8 and rc.ch8 <= 1300  ) then 
	random group 1 200 10					 
	bar group 6 #ff0000 25  0 100  1   
  bar group 7 #00ff00 25  0 100  1     
	bar group 8 #ff0000 25  0 100  1     
	bar group 9 #00ff00 25  0 100  1   		
end
  
if ( 1300 < rc.ch8 and rc.ch8 <= 1400 ) then       // scotts nav pattern
	color group 1 #000000						 
	fill group 6 #ff0000 150 1000 1
	fill group 7 #00ff00 150 1000 1
	fill group 8 #ff0000 150 1000 0
	fill group 9 #00ff00 150 1000 0
	flash group 1 [ 100 100 100 ] 30 970   0							 
	flash group 1 [ 100 100 100 ] 20 980 200		
end
  
if ( 1400 < rc.ch8 and rc.ch8 <= 1500 ) then
	random group 1 200 10					 
	wave group 6 #ff0000 50 2	1
	wave group 7 #00ff00 50 2  1
	wave group 8 #ff0000 50 2  0
	wave group 9 #00ff00 50 2  0			  
end

if ( 1500 < rc.ch8 and rc.ch8 <= 1600 ) then 
	color group 1 #050505		
	bounce group 6 #ff0000 100 1                   // front left
	bounce group 7 #00ff00 100 1                   // front right
	bounce group 8 #ff0000 100 1                   // rear left
	bounce group 9 #00ff00 100 1   				         // rear right
end
  
if ( 1600 < rc.ch8 and rc.ch8 <= 1700 ) then
	color group  1 #000000						 
	flash group 10 #ff0000 20 950   0     
	flash group 10 #ff0000 20 950 100       
	flash group 11 #00ff00 20 950 200        
	flash group 11 #00ff00 20 950 300   							
end
  
if ( 1700 < rc.ch8 and rc.ch8 <= 1800 ) then   
  color group 3 [85 85 85]                      // Front white						 
  color group 4 [85 0 0]                        // Back mid red					 
  if ( imu.brake > 20 ) then                    // Braking
    color group 4 [255 0 0]                     // Back bright red					 
  end   
end
  
if ( 1800 < rc.ch8 and rc.ch8 <= 1900 ) then 
	color group 1 #000000						// off	
end  
  
if ( 1900 < rc.ch8 ) then 
	color group 1 [ 200 200 200]						// ~80% White	
	bar group 6 [ 255 0 0 ] 25 0 100  1   
  bar group 7 [ 0 255 0 ] 25 0 100  1     
	bar group 8 [ 255 0 0 ] 25 0 100  1     
	bar group 9 [ 0 255 0 ] 25 0 100  1   	  
end